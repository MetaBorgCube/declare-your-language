module names

imports 
  src-gen/signatures/Programs-sig
  src-gen/signatures/Expressions-sig
  src-gen/signatures/Classes-sig

namespaces Program Class Field Method Variable
	
binding rules // programs

  Program(name, _, _) :   
  	defines Program name
  	scopes Class, Variable

binding rules // classes

  Class(c, _, _, _) :
  	defines Class c of type ClassT(c)
  	scopes Field, Method, Variable
  	implicitly defines Variable This() of type ClassT(c)
  	
  Extends(c) : 
  	imports Field, Method from Class c

  ClassT(c) :
  	refers to Class c
  	
  New(c) : 
  	refers to Class c
	
  Null(c) :
  	refers to Class c

  	
  Cast(c, e) :
  	refers to Class c
  	 	
binding rules // fields

  Field(t, f) :
  	defines Field f of type t//'
  	//where t has type t'
  	
  Get(e, f) :
  	refers to Field f in Class c
  	where e has type ClassT(c) 
  	
  Set(e1, f, e2) :
  	refers to Field f in Class c
  	where e1 has type ClassT(c)

binding rules // methods
  	
  Method(t, m, ps, e) :
  	defines Method m of type (t*, t)
  	where ps has type t*
  	scopes Variable
  	
  Call(e1, m, e*) :
  	refers to Method m in Class c
  	where e1 has type ClassT(c)
  	
binding rules // variables
 	
  Param(t, x) :
  	defines Variable x of type t
  	
  Let(bs, e) :
  	scopes Variable
  
  Bind(t, x, e) : 
  	defines Variable x of type t
 	
  This() :
  	refers to Variable This()
  	
  Var(x) :
  	refers to Variable x
  	